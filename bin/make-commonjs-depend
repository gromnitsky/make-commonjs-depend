#!/usr/bin/env coffee
# -*-coffee-*-

fs = require 'fs'
optparse = require 'optparse'

class Meta
  @_Pkg: null

  @JSON: ->
    if @_Pkg?
      @_Pkg
    else
      @_Pkg = JSON.parse (fs.readFileSync "#{__dirname}/../package.json")

tree = require '../lib/tree'
fub = require '../lib/funcbag'
printer = require '../lib/printer'

conf = {
    output: 'stdout'
    mode: 'makefile'
    prefix: ''
}

parse_opts = (src) ->
  opt = [
    ['-h', '--help', 'output usage information & exit']
    ['-V', '--version', 'output the version number & exit']
    ['-v', '--verbose', 'increase a verbosity level (useful only for debug)']
    ['-o', '--output [FILE]', 'write result to a FILE instead of stdout']
    ['-p', '--prefix [STRING]', 'the prefix is prepended to the name of the target ']
    ['-m', '--mode [STRING]', 'makefile, tree-dumb']
  ]
  p = new optparse.OptionParser opt
  p.banner = "Usage: #{fub.pnGet()} [options] file.js ..."

  p.on 'verbose', -> fub.VERBOSE++

  p.on 'help', ->
    console.log p.toString()
    process.exit 0

  p.on 'version', ->
    console.log Meta.JSON().version
    process.exit 0

  p.on 'output', (unused, val) -> conf.output = val

  p.on 'prefix', (unused, val) -> conf.prefix = val

  p.on 'mode', (unused, val) -> conf.mode = val

  p.on (o) -> fub.errx 1, "unknown option #{o}"

  [(p.parse src), p]

fnodes_each = (args, beforeCallback, afterCallback) ->
  resolvedNodes = {}
  for file,index in args
    beforeCallback index

    ft = new tree.FTree resolvedNodes
    try
      ft.breed file
    catch e
      fub.errx 1, e.message if e instanceof tree.FNodeDepError
      throw e

    afterCallback ft

draw_tree = (args) ->
  fnodes_each args
  , (index) ->
    console.log "" if index
  , (ftree) ->
    (new printer.DumbTreePrinter ftree).print()

draw_makefile = (args) ->
  completedJobs = {}

  fnodes_each args
  , (index) ->
    ;
  , (ftree) ->
    new printer.MakefilePrinter(ftree, {prefix: conf.prefix}, completedJobs).print()

# main

[args, p] = parse_opts process.argv
args = args[2..-1]
if args.length < 1
  console.log p.toString()
  process.exit 1

switch conf.mode
  when 'tree-dumb'
    draw_tree args
  when 'makefile'
    draw_makefile args
  else
    fub.errx 1, "mode #{conf.mode} isn't implemented"
